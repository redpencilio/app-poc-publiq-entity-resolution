PREFIX :    <http://example.com/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

###################
# Rewrite objects #
###################

DELETE {
    GRAPH <http://organisatorensparql.publiq-uit-organisatoren.vrijetijdsparticipatie.publiq.be> {
        ?s ?p ?bnode .
    }
}
INSERT {
    GRAPH <http://organisatorensparql.publiq-uit-organisatoren.vrijetijdsparticipatie.publiq.be> {
        ?s ?p ?iri .
    }
}
WHERE {
  {
    SELECT ?bnode ?iri
    WHERE {
      {
        SELECT DISTINCT ?bnode
        FROM <http://organisatorensparql.publiq-uit-organisatoren.vrijetijdsparticipatie.publiq.be>
        WHERE {
          [] ?p ?bnode .
          FILTER isBlank(?bnode)
        }
        LIMIT 100000
      }
      OPTIONAL {
        GRAPH :aliases {
          ?bnode owl:sameAs ?_iri .
        }
      }
      BIND (coalesce(?_iri, uuid()) AS ?iri)
    }
  }
    GRAPH <http://organisatorensparql.publiq-uit-organisatoren.vrijetijdsparticipatie.publiq.be> {
        ?s ?p ?bnode .
    }
}
