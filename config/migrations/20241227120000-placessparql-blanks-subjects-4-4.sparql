PREFIX :    <http://example.com/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

# From https://stackoverflow.com/a/47532851

####################
# Rewrite subjects #
####################

DELETE {
    GRAPH <http://placessparql.publiq-uit-locaties.vrijetijdsparticipatie.publiq.be> {
        ?bnode ?p ?o .
    }
}
INSERT {
    GRAPH <http://placessparql.publiq-uit-locaties.vrijetijdsparticipatie.publiq.be> {
        ?iri ?p ?o .
    }
  GRAPH :aliases {
    ?bnode owl:sameAs ?iri .
  }
}
WHERE {
  {
    SELECT ?bnode (uuid() AS ?iri)
    WHERE {
      {
        SELECT DISTINCT ?bnode
        FROM <http://placessparql.publiq-uit-locaties.vrijetijdsparticipatie.publiq.be>
        WHERE {
          ?bnode ?p [] .
          FILTER isBlank(?bnode)
        }
        ORDER BY ?bnode
        LIMIT 100000
      }
    }
  }
    GRAPH <http://placessparql.publiq-uit-locaties.vrijetijdsparticipatie.publiq.be> {
        ?bnode ?p ?o .
    }
}
